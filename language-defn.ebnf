(* BASIC ELEMENTS *)
alpha_upper = 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' |
              'H' | 'I' | 'J' | 'K' | 'L' | 'M' | 'N' |
              'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' |
              'V' | 'W' | 'X' | 'Y' | 'Z' ;
alpha_lower = 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' |
              'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n' |
              'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' |
              'v' | 'w' | 'x' | 'y' | 'z' ;
alpha = alpha_upper | alpha_lower ;
digit_nonzero = '1' | '2' | '3' | '4' | '5' |
                '6' | '7' | '8' | '9' ;
digit = '0' | digit_nonzero ;
identifier = alpha, { alpha | digit } ;
number = '0' | digit_nonzero, { digit } ;

(* MAIN GRAMMAR *)
definition_file = { statement } ;
statement = '(', ( device | connect | monitor ), ')' ;
device = 'DEVICE', identifier, { identifier },
         ('is'|'are'), device_type ;
device_type = device_no_qualifier |
              device_with_qualifier, number ;
device_with_qualifier = 'CLOCK' | 'SWITCH' |
                        'AND' | 'NAND' | 'OR' | 'NOR' ;
device_no_qualifier = 'DTYPE' | 'XOR' ;
connect = 'CONNECT', device_terminal, 'to', device_terminal ;
monitor = 'MONITOR', device_terminal, { device_terminal } ;
device_terminal = identifier, [ '.', identifier ] ;
